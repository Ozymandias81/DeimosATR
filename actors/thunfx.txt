ACTOR Thunder_GeneratorWhite : SwitchableDecoration
{
	//$Category Special Effects
	Height 1
	Radius 1
	Scale 4.0
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+NOCLIP
	+NOGRAVITY
	+NOINTERACTION
	-SOLID
	Renderstyle Add
	Alpha 0.2
	States
	{
	Spawn:
	Active:
		TNT1 A 0 
		TNT1 A 0 A_PlaySoundEx("thunder/hit", "Voice", 0, 2)                
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("Thunder_Spark", 0, 0, 0, random(1,10), 0, random(1,10), random(0,359), 129)
		THND AAAA 1 Bright A_FadeIn(0.2)
		THND AAAA 2 Bright A_FadeOut(0.2)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1  Bright A_SpawnItemEx("Thunder_Smoke", 0, 0, 0, random(0,3), 0, random(2,3), random(0,359), 128)
		Goto Inactive
	Inactive:
		TNT1 A 1
		Loop
	}
}

ACTOR Thunder_GeneratorTan : Thunder_GeneratorWhite {translation "80:111=48:79"}
ACTOR Thunder_GeneratorGreen : Thunder_GeneratorWhite {translation "80:111=112:127"}
ACTOR Thunder_GeneratorRed : Thunder_GeneratorWhite {translation "80:111=171:191"}
ACTOR Thunder_GeneratorBlue : Thunder_GeneratorWhite {translation "80:111=192:207"}
ACTOR Thunder_GeneratorPink : Thunder_GeneratorWhite {translation "80:111=16:47"}

ACTOR Thunder_Spark
{
	Speed 3
	Height 2
	Radius 1
	Mass 0
	+LOWGRAVITY
	+MISSILE
	RenderStyle Add
	Scale 0.4
	States
	{
	Spawn:
		TNT1 A 0
		SPRK A 1 A_Jump(80, 4)              
		SPRK A 1 A_Jump(80, 4)              
		SPRK A 1 A_Jump(80, 4)              
		SPRK A 32 Bright
		SPRK A 32 Bright
		SPRK A 32 Bright
		SPRK A 32 Bright
		SPRK ABCDEFGHHH 1 Bright A_FadeOut(0.1)
		Stop
	Death:
		SPRK ABCDEFGHHH 1 Bright A_FadeOut(0.1)
		Stop		
	}
}

ACTOR Thunder_Smoke
{
	Height 2
	Radius 1
	+MISSILE
	+NOBLOCKMAP
	+NOGRAVITY
	+NOINTERACTION
	RenderStyle Translucent
	Alpha .5
	Scale .65
	States
	{
	Spawn:
		SMK1 IHGFEEDDCCBBBBAAAAAA 2 A_FadeOut(0.05) 
		Stop
	}
}