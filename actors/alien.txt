// Using Demon as Alien Warrior

Actor AlienWarrior : Demon replaces Demon
{
  Speed 18
  Health 100
  MaxStepHeight 40
  MaxDropOffHeight 40
  Radius 16
  Height 32
  GibHealth 0
  BloodColor "yellow"
  Scale 0.9
  Painchance 200
  DamageFactor "Flamer", 2.0
  Obituary "%o was killed by an Alien."
  Hitobituary "%o was killed by an Alien."
  +DROPOFF
  +FLOORCLIP
  +DONTHARMSPECIES
  +DONTHARMCLASS
  +JUMPDOWN
  +NOINFIGHTING
  +THRUSPECIES
  +CANUSEWALLS
  +NEVERRESPAWN
  +NEVERFAST
  +MISSILEMORE
  
  SeeSound "Warrior/sight"
  AttackSound "Warrior/melee"
  PainSound "Warrior/pain"
  DeathSound "Warrior/death"
  ActiveSound "Warrior/active"
  
  States
  {
  Spawn:
	SARG A 0 ACS_ExecuteWithResult(100,0)
    SARG E 8 A_Look
    Loop
  See:
	SARG A 0 Fast A_FaceTarget
    SARG AA 2 Fast A_Chase (0, "Missile")
	SARG A 0 A_PlaySound ("Warrior/run", CHAN_BODY,0.5)
	SARG BB 2 Fast A_Chase ("Melee", 0)
	SARG CC 2 Fast A_Chase ("Melee", 0)
	SARG A 0 A_PlaySound ("Warrior/run", CHAN_BODY,0.5)
	SARG DD 2 Fast A_Chase (0, 0, CHF_FASTCHASE)
	SARG A 0 Fast A_FaceTarget
    SARG AA 2 Fast A_Chase (0, "Missile")
	SARG A 0 A_PlaySound ("Warrior/run", CHAN_BODY,0.5)
	SARG BB 2 Fast A_Chase ("Melee", 0)
	SARG CC 2 Fast A_Chase ("Melee", 0)
	SARG A 0 A_PlaySound ("Warrior/run", CHAN_BODY,0.5)
	SARG DD 2 Fast A_Chase (0, 0, CHF_FASTCHASE)
	SARG A 0 A_ChangeFlag ("FRIGHTENED", FALSE)
    Loop
  Missile:
	SARG A 4
	SARG A 0 Fast A_FaceTarget
	SARG A 0 A_PlaySound ("Warrior/jump", CHAN_6)
	SARG A 1 ThrustThingZ (0, 16, 0, 0)
	SARG A 1 ThrustThing(angle*256/360, 32, 0, 0)
	Goto Melee
  Melee:
    SARG EF 2 Fast A_FaceTarget
	SARG A 0 A_PlaySound ("Warrior/active", CHAN_7)
    SARG G 4 Fast A_SargAttack
	SARG FE 2
	SARG A 2
    Goto See
  Pain:
	SARG A 0 A_ChangeFlag ("FRIGHTENED", TRUE)
    SARG H 8 Fast A_Pain
    Goto See
  Death:
    SARG I 4
    SARG J 4 A_Scream
    SARG K 4 A_Explode (16,72)
    SARG L 4 A_NoBlocking
    SARG M 4 A_PlaySound("misc/gibbed",CHAN_BODY)
    SARG N -1
    Stop
  Pain.Flamer:
    TNT1 A 0 ACS_NamedExecuteAlways("BurnTranslate")
	TNT1 A 0 A_ChangeFlag ("FRIGHTENED", TRUE)
	SARG HHHH 2 Light("Flash") {
		A_Pain;
		A_SpawnItemEX("ItBurns",0,0,0,random(-2,2),0,4,random(60,120));
		}
	SARG AABBCCDD 2 Light("Flash") {
		A_Wander;
		A_SpawnItemEX("ItBurns",0,0,0,random(-2,2),0,4,random(60,120));
		}
	TNT1 A 0 A_ChangeFlag ("FRIGHTENED", FALSE)
	TNT1 A 0 ACS_NamedExecuteAlways("ResetTranslate")
	SARG AABBCCDD 2 {
		A_Wander;
		A_SpawnItemEX("Smoke",0,0,0,random(-2,2),0,4,random(60,120));
		}
    Goto See
  Death.Flamer:
    TNT1 A 0 ACS_NamedExecuteAlways("AshTranslate")
	SARG HHHHH 1 Light("Flash") A_SpawnItemEX("ItBurns",0,0,0,random(-2,2),0,4,random(60,120))
    SARG I 5 {
		A_Scream;
		A_SpawnItemEX("ItBurns",0,0,0,random(-2,2),0,4,random(60,120));
		A_SpawnItemEx("FlameSparks",0,0,0,frandom(-1,1),frandom(-1,1),0,180);
		}
    SARG J 5 {
		A_NoBlocking;
		A_SpawnItemEX("ItBurns",0,0,0,random(-2,2),0,4,random(60,120));
		A_SpawnItemEx("FlameSparks",0,0,0,frandom(-1,1),frandom(-1,1),0,180);
		A_PlaySound("misc/gibbed",CHAN_BODY);
		}
    SARG KLM 5 Light("Flash") A_SpawnItemEX("ItBurns",0,0,0,random(-2,2),0,4,random(60,120))
    SARG NNNNNNNNNN 5 A_SpawnItemEX("Smoke",0,0,0,random(-2,2),0,4,random(60,120))
	SARG N -1
    Stop
	/*
	Raise:
    SARG N 5
    SARG MLKJI 5
    Goto See
	*/
  }
}